// Requires _groupId, _artifactId, _version

ext {
    bintrayRepo = 'maven'
    bintrayName = _groupId

    publishedGroupId = _groupId
    libraryName = _artifactId
    artifact = _artifactId

    siteUrl = 'https://github.com/gaborbiro/prefsutil'
    gitUrl = 'https://github.com/example/prefsutil.git'

    libraryVersion = _version
    libraryDescription = 'Encrypted preferences wrapper for Android'

    developerId = 'bgaborchen'
    developerName = 'Gabor Biro'
    developerEmail = 'bgaborchen@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

apply plugin: 'com.github.dcendents.android-maven'

group = publishedGroupId

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId publishedGroupId
                artifactId artifact

                name libraryName
                description libraryDescription
                url siteUrl

                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'

version = libraryVersion

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

artifacts {
    archives sourcesJar
}

def getBintrayUser() {
    if (System.getenv('BINTRAY_USER')) {
        logger.lifecycle(System.getenv('BINTRAY_USER'))
        return System.getenv('BINTRAY_USER')
    } else if (rootProject.file('local.properties').exists()) {
        Properties properties = new Properties()
        properties.load(rootProject.file('local.properties').newDataInputStream())
        return properties.getProperty('bintray.user')
    } else {
        logger.lifecycle("Warning: Could not find BINTRAY_USER in environment or local.properties")
    }
}

def getBintrayKey() {
    if (System.getenv('BINTRAY_KEY')) {
        logger.lifecycle(System.getenv('BINTRAY_KEY'))
        return System.getenv('BINTRAY_KEY')
    } else if (rootProject.file('local.properties').exists()) {
        Properties properties = new Properties()
        properties.load(rootProject.file('local.properties').newDataInputStream())
        return properties.getProperty('bintray.apikey')
    } else {
        logger.lifecycle("Warning: Could not find BINTRAY_KEY in environment or local.properties")
    }
}

bintray {
    user = getBintrayUser()
    key = getBintrayKey()

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = false
            }
        }
    }
}